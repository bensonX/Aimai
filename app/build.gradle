apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.ins.aimai"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 17
        versionName "1.0.0.17"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //使用矢量图兼容库
        vectorDrawables.useSupportLibrary = true
        ///////////////////////jpush配置 start
        ndk {
            //选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi', 'armeabi-v7a', 'armeabi-v8a'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
        }
        manifestPlaceholders = [
                JPUSH_PKGNAME : applicationId,
                JPUSH_APPKEY : "bd2aae98f3b3cdd64120a050", //JPush上注册的包名对应的appkey.7ca5583d3774a0dc97cf9049
                JPUSH_CHANNEL : "developer-default", //暂时填写默认值即可.
        ]
        ///////////////////////jpush配置 end
    }
    //##########################
    //配置keystore签名 start
    //签名秘钥应避免暴露在受版本控制的文件中
    signingConfigs {
        myConfig {
            try {
                storeFile file("liaoinstan.jks")
                storePassword KEYSTORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
            } catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_ALIAS and KEY_PASSWORD in gradle.properties.")
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.myConfig
            buildConfigField "String", "BASE_URL", '"http://139.129.111.76:8110/"'
            buildConfigField "String", "BASE_URL_RES", '"http://139.129.111.76:8110/"'
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.myConfig
            buildConfigField "String", "BASE_URL", '"http://192.168.1.166/"'
            buildConfigField "String", "BASE_URL_RES", '"http://192.168.1.205:8080/"'
        }
    }
    //配置keystore签名 end
    //##########################
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "aimai-" + defaultConfig.versionCode + "-" + defaultConfig.versionName + "-" + variant.buildType.name + ".apk");
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile project(':common')
    //阿里巴巴vlayout
    compile('com.alibaba.android:vlayout:1.0.9@aar') {
        transitive = true
    }
    //SpringView
    compile 'com.liaoinstan.springview:library:1.2.7'
    //加载自定义字体
    compile 'uk.co.chrisjenx:calligraphy:2.3.0'
    //汉字拼音库TinyPinyin核心包，约80KB
    compile 'com.github.promeg:tinypinyin:2.0.3'
    //textdrawable
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    compile project(':lib_playerview')
    compile project(':lib_domain')
    //极光推送
    compile 'cn.jiguang.sdk:jpush:3.0.3'
    // 此处以JPush 3.0.3 版本为例。
    compile 'cn.jiguang.sdk:jcore:1.1.1'
    // 此处以JCore 1.1.1 版本为例。
    compile project(':lib_camera')
    compile project(':lib_sharesdk')
    compile project(':lib_pay')
    //photoView
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    compile project(':lib_version')
}
